=== Chrys_Test === // Debug allows you to jump here
// This knot is only useful in debug

-> Actual_Knot // this redirects to the knot below
// But you can write anything instead so that you don't have to change main_story.ink

=== Actual_Knot === // You can name this however you want

- I was still walking along those familiar tunnels when I recollected my thoughts.

- (intro)
+ [{d_on}]
{stopping:
    - But I was not in the residential districts anymore. -> intro
    - It was way too painfull for me to stay there, and too dangerous anyway. -> intro
    - I had to keep going forward, until making it out of the colony ruins. 
}
* [{m_on}]

-

*[about the tunnels]
I opened these paths myself, most of them I carved with my utmost determination to improve our quality of life.
*[about my life]
I had grown up there, and I chose to be a path carver.
*[about my people]
I must face it now, I've lost my people.

-
+ [{w_on}]

- I was loosing my footing so easily in this place.
*[a little bit of poetry]
Ideas were escaping from my head as easily as if my skull were an old sieve that lets the water out but keeps the silt in, the suffocating thoughts that blocked my breathing.
*[I was feeling demined]
This ominous feeling that going out won't be my salvation haunted me.
** [{w_on}]
But I knew no other way.{alter(despair, 1)}
*[my fate]
As a path carver it was my duty to open the ways to safety, to possibles future.
** [{w_on}]
I had to stop fleeing my destiny.

-
* [{w_on}]

-These thoughts were echoeing in a loop on the stone walls around me. To undermine my morale.

-
* [{w_on}]

- My weary mind was drifting away again.
+[the fear of what is following me]
Knowing the creature's unquenchable thirst keept me moving forward.
+[the fear of what's ahead of me]
I have heard and learnt the stories of the great drowning, yet I was going to it.
++ [{w_on}]
Or I would end up drained by that monster.
+[how was life here before]
Before the crawling horror came, when the water was still flowing through the cracks of our caves, life was pretty peacefull.{alter (nostalgia, 1)}

-
* [{w_on}]

- The creeping thought of that beast never-ending hunt for me just wouldn't leave my head.
- (scary_thoughts)
+ [{d_on}]
{stopping:
    - I just wanted to stop thinking about it. -> scary_thoughts
    - In fact, I better had to, or else it would hurt my psyche. -> scary_thoughts
    - Too late, I was picturing that thing feasting on my loved ones over and over again. {alter(despair, 3)}
}
* [{m_on}]

-
* [{w_on}]

- To add insult to injury, my body was in terrible pain.
*[my back]
The small amount of provisions I managed to pack for this last minute trip was enough to tire my old frame out.
*[my legs]
I was hoping that the varicose veins in my right leg and my swollen feet wouldn't force me to take breaks.
*[my heart]
I was worried this used heart of mine would have trouble keeping up the pace.

-
* [{w_on}]

- I concentrated on lifting my head up and looking at my surroundings.
*[my workplace]
The main building was still standing, but the usual racket around it had died down. -> workplace
*[the ruins]
The ancillary dwelling, public conveniences, and municipal sculptures that had been erected here by skilled craftswomen were all reduced to dust, eroded, trampled down. -> city_ruins
*[the playground]
Seeing the empty, dilapidated children's playground chilled my blood whilst it drew me to it. -> playground

- (workplace)
+ [{d_on}]
{stopping:
    - The large storefront smoothed out of a flawless granite proudly displayed “a quareer within the quarry” as an invitation to visit it one last time. -> workplace
    - Guided by my memories, I crossed the parking lot where the derelict dredgers were laying dormant. -> workplace
    - Wandering around the now-empty offices that used to be busy at all hours of the day and night troubed me more than I thought it would. -> workplace
    - It was in this very hall that I took my vows to serve in the community as a carver of ways, a seeker of trajectories. -> workplace
    - (pickaxe) As I was passing by the service exit, I came across a pickaxe with a lightweight carbon handle, thought it might come in handy and picked it up. -> out_of_the_colony
}
* [{m_on}]


- (city_ruins)
+ [{d_on}]
{stopping:
    - I used to sleep in those simple yet charming accomodities when I was too tired to get home after my shift. -> city_ruins
    - Further on, the half-face of a gem goddess stared at me with pity and it was I who decomposed before her. -> city_ruins
    - I felt the pieces of myself crack and fall at her carefully crafted clay feet. -> city_ruins
    - As I was leaving, I contemplated the crumbs of the bench on which I had kissed my first lover, Mariana. -> city_ruins
    - (trench) Burried under the rubbbles, I dung up Mariana's trench! Could she have made it out? -> out_of_the_colony
}
* [{m_on}]

- (playground)
+ [{d_on}]
{stopping:
    - I used to play there when we first arrived seventy years ago. -> playground
    - I remember the 6 years-old me waiting for the work to be finished, standing in the legs of the craftswomen who were running out of patience. -> playground
    - Later, the teenager I was smoked calming herbs high up the kids' castle roof. -> playground
    - As wise adults, my digging team and I liked eating out here during lunchbreaks, just outside the mainquarters. -> playground
    - (rope) Even though I knew it was best to leave the past in the past, I couldn't resist grabbing the skipping rope that was lying around, and took it with me. -> out_of_the_colony
}
* [{m_on}]

- (out_of_the_colony)
-
* [{w_on}]

- Actually, being here meant I had reached the end of the colony.
+[into the depths]
By the next tunnels I'd be in uncharted territory.
+[ending a chapter]
It was more than time to bid farewell to my past life.
+[I did it]
It made me regain a little hope and faith in my body's abilities.
++ [{w_on}]
Since it had carried me this far, I could go all the way. {alter (hope, 1)}

-
* [{w_on}]


-> END